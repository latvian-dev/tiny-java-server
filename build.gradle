import java.time.Instant

plugins {
	id 'java'
	id 'maven-publish'
}

def ENV = System.getenv()
version = "${project_version}-${ENV.GITHUB_RUN_NUMBER ? 'build.' + ENV.GITHUB_RUN_NUMBER : 'local.' + Instant.now().epochSecond}"
group = 'dev.latvian.apps'
base.archivesName = 'tiny-java-server'

println("Building version: ${version}")

repositories {
	mavenCentral()
}

dependencies {
	// implementation('com.google.code.gson:gson:2.10.1')

	compileOnly('org.jetbrains:annotations:23.0.0')
	testCompileOnly('org.jetbrains:annotations:23.0.0')
}

jar {
	manifest {
		attributes 'Implementation-Version': archiveVersion
	}
}

compileJava {
	options.encoding = "UTF-8"
	options.release.set(21)
	options.compilerArgs << '-parameters' << '-Xmaxerrs' << '1000'
}

java {
	sourceCompatibility = targetCompatibility = '21'
	withSourcesJar()
	// withJavadocJar()
}

tasks.withType(Javadoc) {
	options.addStringOption('Xdoclint:none', '-quiet')
}

test {
	failOnNoDiscoveredTests = false
}

publishing {
	publications {
		mavenIchor(MavenPublication) {
			artifactId = 'tiny-java-server'
			from components.java
		}
	}

	repositories {
		if (ENV.MAVEN_URL && ENV.MAVEN_USERNAME && ENV.MAVEN_TOKEN) {
			maven {
				url = ENV.MAVEN_URL
				credentials {
					username = ENV.MAVEN_USERNAME
					password = ENV.MAVEN_TOKEN
				}
			}
		}
	}
}